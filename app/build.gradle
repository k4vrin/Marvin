plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'kotlinx-serialization'
}

def keystorePropertiesFile= rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        release {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyPassword keystoreProperties['keyPassword']
            keyAlias keystoreProperties['keyAlias']
        }
    }
    namespace 'com.kavrin.marvin'
    compileSdk 32

    defaultConfig {
        applicationId "com.kavrin.marvin"
        minSdk 26
        targetSdk 32
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
        buildConfigField "String", "API_KEY", API_KEY
        buildConfigField "String", "IMDb_API_KEY", IMDb_API_KEY
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
//        useIR = true
        freeCompilerArgs += [
                // Avoid having to stutter experimental annotations all over the codebase
                "-opt-in=androidx.compose.material.ExperimentalMaterialApi",
                "-opt-in=coil.annotation.ExperimentalCoilApi",
                "-opt-in=com.google.accompanist.pager.ExperimentalPagerApi",
                "-opt-in=kotlinx.coroutines.ExperimentalCoroutinesApi",
                "-opt-in=androidx.paging.ExperimentalPagingApi",
                "-opt-in=kotlinx.serialization.ExperimentalSerializationApi",
                "-opt-in=androidx.lifecycle.compose.ExperimentalLifecycleComposeApi",
                "-opt-in=androidx.compose.foundation.ExperimentalFoundationApi",
                "-opt-in=androidx.compose.animation.ExperimentalAnimationApi",
                "-opt-in=kotlinx.coroutines.FlowPreview"
        ]
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_compiler_version
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    kapt {
        arguments {
            arg("room.schemaLocation", "$projectDir/schemas")
        }
        correctErrorTypes true
    }
    hilt {
        enableAggregatingTask = true
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.5.1'
    implementation 'androidx.activity:activity-compose:1.5.1'
    implementation "androidx.compose.ui:ui:$compose_ui_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_ui_version"
    implementation 'androidx.compose.material:material:1.2.0'
    implementation "androidx.compose.compiler:compiler:$compose_compiler_version"
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_ui_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_ui_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_ui_version"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.1"
    androidTestImplementation "androidx.test:runner:1.4.0"
    androidTestImplementation "androidx.test:rules:1.4.0"
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'

    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-compose:2.6.0-alpha01'

    // LeakCanary
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.9.1'

    // Icons
    implementation "androidx.compose.material:material-icons-extended:$compose_ui_version"

    // Compose Navigation
    implementation "androidx.navigation:navigation-compose:2.5.1"

    // Room components
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    implementation "androidx.room:room-paging:$room_version"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.jakewharton.retrofit:retrofit2-kotlinx-serialization-converter:0.8.0"
    implementation(platform("com.squareup.okhttp3:okhttp-bom:5.0.0-alpha.10"))
    implementation("com.squareup.okhttp3:okhttp")
    implementation("com.squareup.okhttp3:logging-interceptor")

    // Paging 3.0
    implementation 'androidx.paging:paging-compose:1.0.0-alpha15'
    implementation "androidx.paging:paging-runtime-ktx:3.1.1"


    // KotlinX Serialization
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.3"

    // DataStore Preferences
    implementation "androidx.datastore:datastore-preferences:1.0.0"

    // Dagger - Hilt
    implementation "com.google.dagger:hilt-android:$dagger_hilt_vesion"
    kapt "com.google.dagger:hilt-android-compiler:$dagger_hilt_vesion"
    kapt 'androidx.hilt:hilt-compiler:1.0.0'
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0'

    // Coil
    implementation("io.coil-kt:coil-compose:2.0.0")

    // Horizontal Pager and Indicators - Accompanist
    implementation "com.google.accompanist:accompanist-pager:$accompanist_version"
    implementation "com.google.accompanist:accompanist-pager-indicators:$accompanist_version"
    // Swipe to Refresh - Accompanist
    implementation "com.google.accompanist:accompanist-swiperefresh:$accompanist_version"
    // System UI Controller - Accompanist
    implementation "com.google.accompanist:accompanist-systemuicontroller:$accompanist_version"
    // Navigation-Animation - Accompanist
    implementation "com.google.accompanist:accompanist-navigation-animation:$accompanist_version"
    // PlaceHolder
//    implementation "com.google.accompanist:accompanist-placeholder:$accompanist_version"

    // onebone compose-collapsing-toolbar
    implementation "me.onebone:toolbar-compose:2.3.4"
}